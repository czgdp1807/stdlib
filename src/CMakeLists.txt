#### Pre-process: .fpp -> .f90 via Fypp

# Create a list of the files to be preprocessed
set(fppFiles
    stdlib_string_type.fypp
    stdlib_strings.fypp
    stdlib_kinds.fypp
    stdlib_ascii.fypp
    stdlib_optval.fypp
    stdlib_specialfunctions_gamma.fypp
    stdlib_version.fypp
    stdlib_quadrature.fypp
    # stdlib_sorting.fypp
    stdlib_linalg.fypp
    stdlib_selection.fypp
    stdlib_math.fypp
)


fypp_f90("${fyppFlags}" "${fppFiles}" outFiles)

set(SRC
    stdlib_error.f90
    stdlib_ascii.f90
    stdlib_optval.f90
    stdlib_specialfunctions.f90
    stdlib_version.f90
    stdlib_array.f90
    stdlib_kinds.f90
    stdlib_strings.f90
    stdlib_quadrature.f90
    # stdlib_sorting.f90
    stdlib_linalg.f90
    stdlib_selection.f90
    stdlib_specialfunctions_gamma.f90
    stdlib_math.f90
    ${outFiles}
)

add_library(${PROJECT_NAME} ${SRC})

set_target_properties(
  ${PROJECT_NAME}
  PROPERTIES
  POSITION_INDEPENDENT_CODE ON
  WINDOWS_EXPORT_ALL_SYMBOLS ON
)

if(CMAKE_Fortran_COMPILER_ID STREQUAL GNU AND CMAKE_Fortran_COMPILER_VERSION VERSION_LESS 10.0)
  target_compile_options(
     ${PROJECT_NAME}
     PRIVATE
     $<$<COMPILE_LANGUAGE:Fortran>:-fno-range-check>
   )
endif()

set(LIB_MOD_DIR ${CMAKE_CURRENT_BINARY_DIR})
# We need the module directory before we finish the configure stage since the
# build interface might resolve before the module directory is generated by CMake
if(NOT EXISTS "${LIB_MOD_DIR}")
  make_directory("${LIB_MOD_DIR}")
endif()

set_target_properties(${PROJECT_NAME} PROPERTIES
    Fortran_MODULE_DIRECTORY ${LIB_MOD_DIR})
target_include_directories(${PROJECT_NAME} PUBLIC
    $<BUILD_INTERFACE:${LIB_MOD_DIR}>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_MODULEDIR}>
)

#if(f18errorstop)
#  target_sources(${PROJECT_NAME} PRIVATE f18estop.f90)
#else()
#  target_sources(${PROJECT_NAME} PRIVATE f08estop.f90)
#endif()

install(TARGETS ${PROJECT_NAME}
        EXPORT ${PROJECT_NAME}-targets
        RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
        ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
)
install(DIRECTORY ${LIB_MOD_DIR} DESTINATION "${CMAKE_INSTALL_MODULEDIR}")
